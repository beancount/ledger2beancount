#!/usr/bin/perl -w

# Copyright (C) 2016 Stefano Zacchiroli <zack@upsilon.cc>
# License: GNU General Public License (GPL), version 3 or above

# convert a ledger-cli "chart of accounts" file to beancount format

use strict;
use warnings;

use File::BaseDir qw/config_home/;
use YAML::XS qw(LoadFile);

my @config_files = ('ledger2beancount.yml', config_home('ledger2beancount', 'config.yml'));
my $config;
foreach my $config_file (@config_files) {
    if (-r $config_file) {
	$config = LoadFile($config_file);
	last;
    }
}
die "No config file found (tried ", (join ", ", @config_files), ")\n" if !$config;

sub print_metadata($$) {
    my ($key, $value) = @_;
    print ' ' x $config->{beancount_indent}, lc($key), ": \"$value\"\n";
}

while (my $l = <>) {
    chomp $l;

    if ($l =~ /^account\s+(.*)/) {  # account declaration
	print "$config->{account_open_date} open $1\n";
    } elsif ($l =~ /^\s+note\s+(.*)/) {  # note
	print_metadata("description", $1);
    } elsif ($l =~ /^\s+;\s*(\S+):\s*(.*)/) {  # metadata
	print_metadata($1, $2);
    } else {
	print "$l\n";
    }
}
